apply plugin: 'kotlin-multiplatform'

//plugins {
//    id 'kotlin-multiplatform' version '1.3.0-rc-57'
//}
repositories {
    mavenLocal()
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    jcenter()
    mavenCentral()
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')

        configure([js]) {
            [tasks.getByName(compilations.main.compileKotlinTaskName), tasks.getByName(compilations.test.compileKotlinTaskName)]*.kotlinOptions {
                moduleKind = "umd"
                sourceMap = false
                metaInfo = false
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.26.1-eap13"

            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
    }
}

dependencies {
    jvmTestImplementation gradleTestKit()
}

// JS TESTS

apply plugin: 'com.moowork.node'

afterEvaluate {
    task populateNodeModules(type: Copy, dependsOn: jsTestClasses) {
        from "${buildDir}/classes/kotlin/js/main"
        from "${buildDir}/classes/kotlin/js/test"

        configurations.jsTestCompileClasspath.each {
            from zipTree(it.absolutePath).matching { include '*.js' }
        }

        into "${buildDir}/node_modules"
    }

    task mochaInstall(type: NpmTask) {
        args = ['install', 'mocha']
    }

    task mochaTest(type: NodeTask, dependsOn: [mochaInstall, populateNodeModules]) {
        script = rootProject.file("$buildDir/../node_modules/mocha/bin/mocha")
        args = [new File("$buildDir/node_modules/${project.name}_test.js"), "--reporter", "dot"]
        execOverrides {
            it.workingDir = buildDir
        }
    }

    jsTest.dependsOn(mochaTest)
}
